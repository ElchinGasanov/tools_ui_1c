
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	Значение = Неопределено;
	Если Параметры.Свойство("ЗначениеПеречисления", Значение) И Значение <> Неопределено Тогда
		Имя = Значение.Метаданные().Имя;
		Тип = "Перечисление." + Имя;                                    
		Заголовок = "Перечисления." + Имя;
		Для каждого ЭлементПеречисления из Значение.Метаданные().ЗначенияПеречисления Цикл
			НоваяСтрока = ЗначенияПеречисления.Добавить();
			НоваяСтрока.Ссылка = ПредопределенноеЗначение(Тип + "." + ЭлементПеречисления.Имя);
			НоваяСтрока.Имя = ЭлементПеречисления.Имя;
			НоваяСтрока.Представление = ЭлементПеречисления.Синоним;
		КонецЦикла;
		НайденныеСтроки = ЗначенияПеречисления.НайтиСтроки(Новый Структура("Ссылка", Значение));
		Если НайденныеСтроки.Количество() Тогда
			Элементы.ЗначенияПеречисления.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗначенияПеречисления

&НаКлиенте
Процедура ЗначенияПеречисленияПриАктивизацииСтроки(Элемент)
	ТекДанные = Элементы.ЗначенияПеречисления.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ЗначениеСтр = Тип + "." + ТекДанные.Имя;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияПеречисленияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыбратьЗначение(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьЗначение(Команда)
	
	ТекДанные = Элементы.ЗначенияПеречисления.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ВыбранноеЗначение = ТекДанные.Ссылка;
	Иначе     
		ВыбранноеЗначение = ПредопределенноеЗначение(Тип + ".ПустаяСсылка");
	КонецЕсли;
	
	ОповеститьОВыборе(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти




